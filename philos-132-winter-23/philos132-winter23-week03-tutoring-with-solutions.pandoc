<style>
table {
  border-collapse: collapse;
  width: 100%;
}

th, td {
  text-align: left;
  padding: 8px;
}

tr:nth-child(even) {background-color: #f2f2f2;}
</style>

<br>
<br>

# Week 3 Tutoring session with solutions

The problems in this set fall into four groups:


- [Translations with many quantifiers](#translations-with-many-quantifiers)
- [Definite descriptions](#definite-descriptions)
- [Building models that make formulas true](#building-models-that-make-formulas-true)
- [Truth and many quantifiers](#truth-and-many-quantifiers)

<br>

This version has **the solutions** filled in. They are below the multiple choice problems and just filled in the solution spot for all the other problems.


## Arrow diagrams and binary relations

*Problems 1-4*

Consider the following four arrow diagrams I,II,III, and IV:

&nbsp; &nbsp; &nbsp;  <img src="https://logic-teaching.github.io/philos-132-winter-23/vid2/tutoring-many-arrrows.png" alt="Six point models" width="700"/>

Recall the convention in arrow diagrams for binary relations: $Ruv$ corresponds to a drawn arrow from $u$ to $v$.

Consider the following four sentences:

- $\exists \; x \; \forall \; y \; Rxy$, which in the diagram means: something points to everything.

- $\exists \; x \; \forall \; y \; Ryx$, which in the diagram means: something is pointed at by everything.

- $\forall \; x \; \exists \; y \; Ryx$, which in the diagram means: everything is pointed at by something.

- $\forall\; x \; \exists\; y \; Rxy$, which in the diagram means: everything points to something

Each of the four diagrams makes true at least one of the four sentences. We make it a little more challenging than [what we did in section](https://carnap.io/shared/logiclogiclogic/philos132-winter23-week03-section-with-solutions.pandoc#arrow-diagrams-and-binary-relations) in that we draw several 'extraneous' arrows in the diagrams which are irrelevant to the four sentences at hand.

<br>

```{.QualitativeProblem .MultipleChoice submission="none" options="check"}
3.01 Which of the following is true in diagram I?
|+ ∃ x ∀ y Rxy
| ∃ x ∀ y Ryx
|+ ∀ x ∃ y Ryx
| ∀ x ∃ y Rxy
```

*Note*: $\exists \; x \; \forall \; y \; Rxy$ is true because $4$ points to every number, and hence one can take $x=4$. It is also true that $\forall \; u \; \exists \; v \; Rvu$, since for each $u=1,2,3,4,5,6$ one can take $v=4$ to make $Rvu$ true. And $\forall \; u \; \exists \; v \; Rvu$ iff $\forall \; x \; \exists \; y \; Ryx$ (we will go over 'change of bound variables' in a future week).

<br>

```{.QualitativeProblem .MultipleChoice submission="none" options="check"}
3.02 Which of the following is true in diagram II?
| ∃ x ∀ y Rxy
| ∃ x ∀ y Ryx
|+ ∀ x ∃ y Ryx
| ∀ x ∃ y Rxy
```

*Note*: One has $\forall \; x \; \exists \; y \; Ryx$ is true because everything is pointed to by something. For instance, $x=1$ is pointed to by $y=6$, and $x=4$ is pointed to by $y=2$, etc.


<br>

```{.QualitativeProblem .MultipleChoice submission="none" options="check"}
3.03 Which of the following is true in diagram III?
| ∃ x ∀ y Rxy
| ∃ x ∀ y Ryx
| ∀ x ∃ y Ryx
|+ ∀ x ∃ y Rxy
```

*Note*: this is true because everything points to something.

<br>


```{.QualitativeProblem .MultipleChoice submission="none" options="check"}
3.04 Which of the following is true in diagram IV?
| ∃ x ∀ y Rxy
|+ ∃ x ∀ y Ryx
| ∀ x ∃ y Ryx
|+ ∀ x ∃ y Rxy
```

*Note*: this is because $6$ is such that everything points to $6$. That is, $\exists \; x \; \forall \; y \; Ryx$ is true because $\forall \; y \; Ry6$. It is also true that $\forall \; u \; \exists \; v \; Ruv$. This is because for each $u=1,2,3,4,5,6$ we can choose $v=6$ to make $Ruv$ true. And one has that $\forall \; u \; \exists \; v \; Ruv$ iff $\forall \; x \; \exists \; y \; Rxy$. (We will cover more 'change of bound variables' in a future lecture).

<br>


<p style="page-break-before: always">

<br>

## Translations with many quantifiers

In these problems we use the

<p style="margin-left: 40px"> $Gx$ = $x$ is a French major </p>
<p style="margin-left: 40px"> $Hx$ = $x$ is a History major </p>
<p style="margin-left: 40px"> $Rxy$ = $x$ is more well-read than $y$  </p>
<p style="margin-left: 40px"> $Txy$ = $x$ is more well-traveled than $y$  </p>


<br>

*Example 5*

~~~{.Translate .FOL system="gamutND" submission="none"}
3.05 ExAyRxy : Someone is more well-read than everyone.
|ExAyRxy
~~~

*Example 6*

~~~{.Translate .FOL system="gamutND" submission="none"}
3.06 ExAy(Fy->Rxy) : Someone is more well-read than all French majors.
|ExAy(Fy->Rxy)
~~~

<br>

*Example 7*

~~~{.Translate .FOL system="gamutND" submission="none"}
3.07 Ex(Hx/\Ay(Fy->Rxy)) : Some History major is more well-read than all French majors.
|Ex(Hx/\Ay(Fy->Rxy))
~~~


<br>

*Example 8*

~~~{.Translate .FOL system="gamutND" submission="none"}
3.08 Ax(Fx->Ey(Hy/\Txy)) : Every French major is more well-traveled than some History major.
|Ax(Fx->Ey(Hy/\Txy))
~~~

<br>

*Example 9*

~~~{.Translate .FOL system="gamutND" submission="none"}
3.09 Ax(Ey(Fy/\Rxy)->Ey(Hy/\Txy)) : Everyone who is more well-read than some French major is more well-traveled than some History major.
|Ax(Ey(Fy/\Rxy)->Ey(Hy/\Txy))
~~~

<p style="page-break-before: always">

<br>


## Definite Descriptions

*Problem 10*

Here is the key, and from this is evident that we are talking about cities:

<p style="margin-left: 40px"> $Lx$ = $x$ is a lawyer </p>
<p style="margin-left: 40px"> $Dx$ = $x$ is a doctor </p>
<p style="margin-left: 40px"> $Txy$ = $x$ trusts $y$

First give the *Russellian translation*:

~~~{.Translate .Desc system="gamutNDDesc" submission="none"}
03.10.1  : The doctor does not trust the lawyer
|Ex(Dx/\(Ay(Dy->y=x)/\Eu(Lu/\Av(Lv->v=u)/\~Txu)))
~~~

Second give the *Fregean translation*:

~~~{.Translate .Desc system="gamutNDDesc" submission="none"}
03.10.2 ~TixDxiuLu : The doctor does not trust the lawyer
|~TixDxiuLu
~~~

<br>

*Problem 11*

Here is the key for these problems-- evidently, we are talking about campuses:

<p style="margin-left: 40px"> $b$ = UC Berkeley </p>
<p style="margin-left: 40px"> $Sxy$ = $x$ is smaller than $y$  </p>
<p style="margin-left: 40px"> $Nx$ = $x$ is new  </p>

First give the *Russellian translation*:

~~~{.Translate .Desc system="gamutNDDesc" submission="none"}
03.11.1 Ex(Nx/\Ay(Ny->y=x)/\Sxb) : The new campus is smaller than UC Berkeley
|Ex(Nx/\Ay(Ny->y=x)/\Sxb)
~~~

Second give the *Fregean translation*:

~~~{.Translate .Desc system="gamutNDDesc" submission="none"}
03.11.2 SixNxb : The new campus is smaller than UC Berkeley
|SixNxb
~~~

<p style="page-break-before: always">

<br>




## Building models that make formulas true

<br>

**A solution** is given for each problem. Keep in mind that there are many good solutions to these problems.

<br>

*Example 12*

Find a model which makes $\forall \; x \; \exists \; y \; (Rxy \wedge Txy)$, and which has at least two constants in it and which interprets $R$ and $T$ differently (this is what the second formula requires).

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.12 AxEy(Rxy/\Txy), (Rab/\~Tab)
|Domain:0,1
|R(_,_):[0,1],[1,1],[1,0]
|T(_,_):[0,1],[1,1]
|a:1
|b:0
```

<br>

*Example 13*

Find a model where the following is true:

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.13 (Fa/\~Ga), (~Fb/\Gb), (Rab/\~Rba)
|Domain:0,1
|F(_):0
|G(_):1
|R(_,_):[0,1]
|a:0
|b:1
```

<br>

*Example 14*

Find a model where the following is true:

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.14 ExEy((Fx/\~Gx)/\((~Fy/\Gy)/\(Rxy/\~Ryx)))
|Domain:0,1
|F(_):0
|G(_):1
|R(_,_):[0,1]
|a:0
|b:1
```

<br>

*Example 15*

Find a model where the following is true:

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.15 ExEy((Fx/\~Gx)/\((~Fy/\Gy)/\(Ryx/\~Rxy)))
|Domain:0,1
|F(_):0
|G(_):1
|R(_,_):[1,0]
```

<br>

*Example 16*

Find a model where the following is true:

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.16 ExEy((Fx/\~Gx)/\((~Fy/\Gy)/\(Ryx/\~Rxy)))
|Domain:0,1
|F(_):0
|G(_):1
|R(_,_):[1,0]
```


<p style="page-break-before: always">

<br>


## Truth and many quantifiers

These problems just ask you to find models where all the displayed sentences are true. They all begin with the formulas $Fa\wedge (Fb\wedge \neg Fc)$ and $(Ga\wedge (\neg Gb \wedge \neg Gc))$. These have the effect of requiring you to make models with at least three distinct elements, since $a$ is required to have both $F$ and $G$, and $b$ has the first but not the second, and $c$ has neither.

<br>

**A solution** is given for each problem. Keep in mind that there are many good solutions to these problems. The domain and the interpretation of the constants and the unary relations are the same in these solutions, and we just vary the interpretation of the binary relation symbol.

<br>

*Example 17*

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.17 (Fa/\(Fb/\~Fc)), (Ga/\(~Gb/\~Gc)), (Raa/\Rab), Ey(~Fy/\(AxRax->AxRyx))
|Domain:0,1,2
|F(_):0,1
|G(_):0
|R(_,_):[0,0],[0,1],[2,0],[2,1]
|a:0
|b:1
|c:2
```

<br>

*Example 18*

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.18 (Fa/\(Fb/\~Fc)), (Ga/\(~Gb/\~Gc)), AxRax, Ay(~Fy->AxRyx)
|Domain:0,1,2
|F(_):0,1
|G(_):0
|R(_,_):[0,0],[0,1],[0,2],[2,0],[2,1],[2,2]
|a:0
|b:1
|c:2
```


<br>

*Example 19*

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.19 (Fa/\(Fb/\~Fc)), (Ga/\(~Gb/\~Gc)), Ax(Ey(Fy/\Rxy)->Ey(Gy/\Ryx))
|Domain:0,1,2
|F(_):0,1
|G(_):0
|R(_,_):[2,0],[0,2]
|a:0
|b:1
|c:2
```

<br>

*Example 20*

```{.CounterModeler .Simple system="gamutND" submission="none"}
03.20 (Fa/\(Fb/\~Fc)), (Ga/\(~Gb/\~Gc)), EyEz(AxRxy/\Ax~Rxz)
|Domain:0,1,2
|F(_):0,1
|G(_):0
|R(_,_):[0,0],[1,0],[2,0]
|a:0
|b:1
|c:2
```

<br>

These are practice problems written by Sean Walsh. They are run on [carnap.io](http://www.carnap.io).[^5]

[^5]: which is:

<br>
